evalExam := procedure(data, maxPoints) {
    state := "normal";
    scan(data) using position {
        regex '[a-zA-Z]+:.*\n': // skip header
        regex '\w+\s[A-Za-z\-]+:' as [ name ]:
              nPrint(name);
              state := "printBlanks";
              sumPoints := 0;
        regex '[ \t]+' as [ whiteSpace ] | state == "printBlanks":
              nPrint(whiteSpace);  
              state := "normal";
        regex '[ \t]+' | state == "normal": 
              // skip white space between points
        regex '0|[1-9][0-9]*' as [ number ]:
              sumPoints += int(number);
        regex '-': 
              // skip exercises that have not been done  
        regex '\n' | sumPoints != om:
              print(note(sumPoints, maxPoints));
              sumPoints := om;
        regex '[ \t]*\n' | sumPoints == om:
              // skip empty lines
        regex '.|\n' as [ c ]:
              print(position["line"],":",position["column"]," unrecognized character '$c$'");
    }
};

note := procedure(p, m) {
    return nbrFormat(7.0 - 6.0 * p / m);
};

nbrFormat := procedure(value) {
    roundValue := 10;
    rVal    := 1.0 * (round(value * roundValue) / roundValue);
    result  := str(rVal);
    n       := #str(roundValue);
    while (#result <= n + 1) {
        result += "0";
    }
    if (#result > n + 1) {
        result := result[1 .. n+1];
    }
    return result;
};

data := "
Exam:    Algorithms and Data Structures
Course:  TIT07AIX

Tasks:                     1. 2. 3. 4. 5. 6.
Max Mueller-Ludenscheidt:  8  9  8  -  7  6
Daniel Dumpfbacke:         4  4  2  0  -  -
Susi Sorglos:              9 12 12  9  9  6
Jacky Jeckle:              9 12 12  -  9  6
";

evalExam(data, 60);

