suits  := { "c", "h", "d", "s" };     
values := { "2", "3", "4", "5", "6", "7", "8", "9", "T", "J", "Q", "K", "A" }; 
// deck ist die Menge aller Karten.
deck   := { [ v, s ] : v in values, s in suits };
// hole ist die Menge der beiden Karten, die der Spieler erhalten hat.
hole   := { [ "8", "s" ], [ "9", "s" ] };
// rest ist die Menge aller übrigen Karten.
rest   := deck - hole;
// flops ist die Menge aller möglichen Flops
flops  := { { k1, k2, k3 } : k1 in rest, k2 in rest, k3 in rest 
                           | #{ k1, k2, k3 } == 3 };
// twoSpades ist die Menge aller Flops, die mindestens zwei Pik enthalten.
twoSpades := { f : f in flops | #{ k : k in f | k[2] == "s" } >= 2 };
print(1.0 * #twoSpades / #flops);

// threeSpades ist die Menge aller Flops, die drei Pik enthalten.
threeSpades := { f : f in flops | #{ k : k in f | k[2] == "s" } == 3 };
print(1.0 * #threeSpades / #flops);

